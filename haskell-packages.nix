{ pkgs
, forceDontCheck
, enableProfiling
, enablePhaseMetrics
, enableHaddockHydra
, enableBenchmarks
, enableSplitCheck
, fasterBuild
, enableDebugging
, filter
, requiredOverlay
, pkgsGenerated
, ghc ? pkgs.haskell.compiler.ghc822
}:

with pkgs.lib;

let

  # This will yield a set of haskell packages, based on the given compiler.
  pkgsBase = ((import pkgsGenerated { inherit pkgs; }).override {
    inherit ghc;
  });

  # Overlay logic for *haskell* packages.
  requiredOverlay'    = import requiredOverlay              { inherit pkgs ; };
  benchmarkOverlay    = import ./overlays/benchmark.nix     { inherit pkgs filter; };
  profilingOverlay    = import ./overlays/profile.nix       { inherit pkgs filter; };
  debugOverlay        = import ./overlays/debug.nix         { inherit pkgs; };
  fasterBuildOverlay  = import ./overlays/faster-build.nix  { inherit pkgs filter; };
  dontCheckOverlay    = import ./overlays/dont-check.nix    { inherit pkgs; };
  metricOverlay       = import ./overlays/metric.nix        { inherit pkgs; };
  haddockHydraOverlay = import ./overlays/haddock-hydra.nix { inherit pkgs filter; };
  splitCheckOverlay   = import ./overlays/split-check.nix   { inherit pkgs filter; };

  activeOverlays = [ requiredOverlay' ]
      ++ optional enableProfiling profilingOverlay
      ++ optional enablePhaseMetrics metricOverlay
      ++ optional enableBenchmarks benchmarkOverlay
      ++ optional enableDebugging debugOverlay
      ++ optional forceDontCheck dontCheckOverlay
      ++ optional forceDontCheck splitCheckOverlay
      ++ optional forceDontCheck haddockHydraOverlay
      ++ optional fasterBuild fasterBuildOverlay;

in
  # Apply all the overlays on top of the base package set generated by stack2nix
  builtins.foldl' (pkgs: overlay: pkgs.extend overlay) pkgsBase activeOverlays
